  //%attributes = {"invisible":true,"lang":"fr"} comment added and reserved by 4D.
  // --[HEADERSTART]-------------------------------------
  // User name (OS): Joshua Fletcher
  // Date and time: 08/16/13
  // ----------------------------------------------------
  // Description:
  //   Convert 4D form to JSON.
  //
  //   Naming convention for JSON attributes: take the 4D
  //   name, make it camel case, remove spaces.
  //
  //   Underscore indicates extra info.  Example: "type"
  //   is a property and internally has a numberic value.
  //   "type_label" stores a text label for the type;
  //   it's metadata.
  //
  //   If an attribute starts with an underscore, there is
  //   no corresponding property in 4D.
  //
  //   http://google-styleguide.googlecode.com/svn/trunk/jsoncstyleguide.xml#Property_Name_Guidelines
  // --[HEADEREND]---------------------------------------
C_OBJET($1;$parameterList_o)

C_ENTIER LONG($0;$error_l)

C_POINTEUR($currentTable_p;$subformTable_p;$currentObjectPointer_p)
C_TEXTE($currentFormName_t;$currentTable_t;$windowTitle_t;$currentObjectName_t;$shortCutKey_t;$detailSubform_t;$listSubform_t;$varName_t)
C_ENTIER LONG($currentTable_l;$width_l;$height_l;$numPages_l;$bestWidth_l;$bestHeight_l;$foregroundColor_l;$backgroundColor_l;$altBackgrndColor_l;$shortcutModifiers_l;$variableType_l;$minimumValue_r;$minWidth_l;$maxWidth_l;$minHeight_l;$maxHeight_l;$tableNum_l;$fieldNum_l)
C_BOOLÉEN($fixedWidth_f;$fixedHeight_f;$draggable_f;$automaticDrag_f;$droppable_f;$automaticDrop_f;$printFrame_f;$horizontalScrollBar_f;$verticalScrollBar_f;$horizontalResize_f;$verticalResize_f)
C_RÉEL($maximumValue_r)
C_DATE($maximumValue_d;$minimumValue_d)
C_HEURE($maximumValue_h;$minimumValue_h)
C_TEXTE($maximumValue_t;$minimumValue_t)

C_OBJET($currentForm_o)
C_OBJET($currentObjects_o)
C_OBJET($currentObject_o)
C_OBJET($parameterList_o)

TABLEAU TEXTE($objectNames_at;0)
TABLEAU POINTEUR($variablesArray_ap;0)
TABLEAU ENTIER LONG($pagesArray_al;0)
TABLEAU OBJET($formEvents_ao;0)
TABLEAU OBJET($objectEvents_ao;0)

$parameterList_o:=$1

$currentFormName_t:=OB Lire($parameterList_o;<>VCF_KEY_ParamFormName_t)
$currentForm_o:=OB Lire($parameterList_o;<>VCF_KEY_ParamForm_t)

OB FIXER($currentForm_o;<>VCF_KEY_ParamFormName_t;$currentFormName_t)

  // This block is to access Form Properties.
Si (OB Est défini($parameterList_o;<>VCF_KEY_ParamTableNum_t))
	  // Table form.
	$currentTable_l:=OB Lire($parameterList_o;<>VCF_KEY_ParamTableNum_t)
	$currentTable_p:=Table($currentTable_l)
	$currentTable_t:=Nom de la table($currentTable_p)
	OB FIXER($currentForm_o;"_table";$currentTable_t)  // "table" isn't really a property, it's the owner, so flag this as metadata with _
	  // FORM GET PROPERTIES does not work on a host form currently.
	Si (Non(UTIL_IsComponent ))
		FORM LIRE PROPRIÉTÉS($currentTable_p->;$currentFormName_t;$width_l;$height_l;$numPages_l;$fixedWidth_f;$fixedHeight_f;$windowTitle_t)
	Fin de si 
	FORM CHARGER($currentTable_p->;$currentFormName_t;*)
Sinon 
	  // Project form.
	  // FORM GET PROPERTIES does not work on a host form currently.
	Si (Non(UTIL_IsComponent ))
		FORM LIRE PROPRIÉTÉS($currentFormName_t;$width_l;$height_l;$numPages_l;$fixedWidth_f;$fixedHeight_f;$windowTitle_t)
	Fin de si 
	FORM CHARGER($currentFormName_t;*)
Fin de si 


OB FIXER($currentForm_o;"_width";$width_l)
OB FIXER($currentForm_o;"_height";$height_l)
OB FIXER($currentForm_o;"_numPages";$numPages_l)
OB FIXER($currentForm_o;"_fixedWidth";$fixedWidth_f)
OB FIXER($currentForm_o;"_fixedHeight";$fixedHeight_f)
OB FIXER($currentForm_o;"windowTitle";$windowTitle_t)


  // Pass empty string to get Form Events.
  // REMINDER: if there is no form method, this command returns no events.
VCF_OBJ_SetEvents ($currentForm_o;"")

FORM LIRE REDIMENSIONNEMENT HORIZONTAL($horizontalResize_f;$minWidth_l;$maxWidth_l)
FORM LIRE REDIMENSIONNEMENT VERTICAL($verticalResize_f;$minHeight_l;$maxHeight_l)


OB FIXER($currentForm_o;"fixedWidth";Non($horizontalResize_f))
OB FIXER($currentForm_o;"minWidth";$minWidth_l)
OB FIXER($currentForm_o;"maxWidth";$maxWidth_l)
OB FIXER($currentForm_o;"fixedHeight";Non($verticalResize_f))
OB FIXER($currentForm_o;"minHeight";$minHeight_l)
OB FIXER($currentForm_o;"maxHeight";$maxHeight_l)


FORM LIRE OBJETS($objectNames_at;$variablesArray_ap;$pagesArray_al)

C_ENTIER LONG($k;$numObjects_l)

$numObjects_l:=Taille tableau($objectNames_at)

  // Loop over Objects
Si ($numObjects_l>0)
	EFFACER VARIABLE($currentObjects_o)
	
	Boucle ($k;1;$numObjects_l)
		EFFACER VARIABLE($currentObject_o)
		
		$currentObjectName_t:=$objectNames_at{$k}
		
		OB FIXER($currentObject_o;"objectName";$currentObjectName_t)
		
		  //== OBJECT Get type ========================================================
		VCF_OBJ_SetPropWithLabel ($currentObjectName_t;"type";$currentObject_o)
		
		
		  //== OBJECT Get pointer =====================================================
		  // This works with FORM LOAD but note I also already have a pointer to the object from FORM GET OBJECTS.
		  // Of course I don't actually care about the pointer, I care about what it points to.
		$currentObjectPointer_p:=OBJET Lire pointeur(Objet nommé;$currentObjectName_t)
		Si (Non(Pointeur nil($currentObjectPointer_p)))
			RÉSOUDRE POINTEUR($currentObjectPointer_p;$varName_t;$tableNum_l;$fieldNum_l)
			Au cas ou 
				: ($varName_t#"")  // Variable.
					  // If a Form Object has no value in the Variable Name property, 4D assigns 
					  // one automatically when the form is loaded via FORM LOAD. The value 
					  // changes every time the form is loaded. This seems somewhat useless 
					  // information in a Revision Control context so I will ignore these values.
					  // I would rather know that the value is blank. 
					  // As far as I'm aware, you should never have a legitimate Object with a 
					  // local variable name so it seems safe to filter out anything that starts 
					  // with $ (for example 4D will use $form.34.5E).
					Si ($varName_t="$@")
						$varName_t:=""
					Fin de si 
					OB FIXER($currentObject_o;"variableName";$varName_t)
					$variableType_l:=Type($currentObjectPointer_p->)  // This is used for Max/Min value later.
					VCF_OBJ_SetPropWithLabel ($currentObjectName_t;"variableType";$currentObject_o)
				: ($fieldNum_l#0)  // Field.
					OB FIXER($currentObject_o;"source";Nom de la table($tableNum_l))
					OB FIXER($currentObject_o;"sourceField";Nom du champ($currentObjectPointer_p))
					$variableType_l:=Type($currentObjectPointer_p->)  // This is used for Max/Min value later.
					VCF_OBJ_SetPropWithLabel ($currentObjectName_t;"_fieldType";$currentObject_o)
				: (($tableNum_l#0) & ($fieldNum_l=0))  // Subform displaying a table.
					OB FIXER($currentObject_o;"source";Nom de la table($tableNum_l))
					$variableType_l:=Est une variable indéfinie  // This is used for Max/Min value later.
				Sinon 
					$variableType_l:=Est une variable indéfinie  // This is used for Max/Min value later.
			Fin de cas 
		Sinon 
			$variableType_l:=Est une variable indéfinie  // This is used for Max/Min value later.
		Fin de si 
		
		
		  // NOTE: The rest of the getters are accessed alphabetically.
		
		
		  //== OBJECT Get action ======================================================
		VCF_OBJ_SetPropWithLabel ($currentObjectName_t;"action";$currentObject_o)
		
		
		  //== OBJECT Get auto spellcheck =============================================
		OB FIXER($currentObject_o;"autoSpellcheck";OBJET Lire correction orthographique(*;$currentObjectName_t))
		
		
		  //== OBJECT GET BEST SIZE ===================================================
		OBJET LIRE TAILLE OPTIMALE(*;$currentObjectName_t;$bestWidth_l;$bestHeight_l)
		OB FIXER($currentObject_o;"width";$bestWidth_l)
		OB FIXER($currentObject_o;"height";$bestHeight_l)
		
		
		  //== OBJECT Get border style ================================================
		VCF_OBJ_SetPropWithLabel ($currentObjectName_t;"borderLineStyle";$currentObject_o)
		
		
		  //== OBJECT Get context menu ================================================
		OB FIXER($currentObject_o;"contextMenu";OBJET Lire menu contextuel(*;$currentObjectName_t))
		
		
		  //== OBJECT GET COORDINATES =================================================
		OBJET LIRE COORDONNÉES(*;$currentObjectName_t;$left_l;$top_l;$right_l;$bottom_l)
		OB FIXER($currentObject_o;"left";$left_l)
		OB FIXER($currentObject_o;"top";$top_l)
		OB FIXER($currentObject_o;"right";$right_l)
		OB FIXER($currentObject_o;"bottom";$bottom_l)
		
		
		  //== OBJECT Get data source =================================================
		  // Doesn't do much that's useful with FORM LOAD. Variables are undefined.
		
		
		  //== OBJECT GET DRAG AND DROP OPTIONS =======================================
		OBJET LIRE OPTIONS GLISSER DÉPOSER(*;$currentObjectName_t;$draggable_f;$automaticDrag_f;$droppable_f;$automaticDrop_f)
		OB FIXER($currentObject_o;"draggable";$draggable_f)
		OB FIXER($currentObject_o;"automaticDrag";$automaticDrag_f)
		OB FIXER($currentObject_o;"droppable";$droppable_f)
		OB FIXER($currentObject_o;"automaticDrop";$automaticDrop_f)
		
		
		  //== OBJECT Get enabled =====================================================
		OB FIXER($currentObject_o;"enabled";OBJET Lire activation(*;$currentObjectName_t))
		
		
		  //== OBJECT Get enterable ===================================================
		OB FIXER($currentObject_o;"enterable";OBJET Lire saisissable(*;$currentObjectName_t))
		
		
		  //== OBJECT GET EVENTS ======================================================
		  // REMINDER: if there is no object method, this command returns no events.
		VCF_OBJ_SetEvents ($currentObject_o;$currentObjectName_t)
		
		
		  //== OBJECT Get filter ======================================================
		OB FIXER($currentObject_o;"filter";OBJET Lire filtre saisie(*;$currentObjectName_t))
		
		
		  //== OBJECT Get focus rectangle invisible ===================================
		OB FIXER($currentObject_o;"focusRectangleInvisible";OBJET Lire rectangle focus invisible(*;$currentObjectName_t))
		
		C_ENTIER LONG($type)
		$type:=OBJET Lire type(*;$currentObjectName_t)
		Si ($type#Objet type zone write pro)
			  //== OBJECT Get font ========================================================
			
			
			OB FIXER($currentObject_o;"font";OBJET Lire police(*;$currentObjectName_t))
			
			
			
			
			
			  //== OBJECT Get font size ===================================================
			OB FIXER($currentObject_o;"fontSize";OBJET Lire taille police(*;$currentObjectName_t))
			
			
			  //== OBJECT Get font style ==================================================
			OB FIXER($currentObject_o;"fontStyle";OBJET Lire style police(*;$currentObjectName_t))
			
			
			  //== OBJECT Get format ======================================================
			OB FIXER($currentObject_o;"format";OBJET Lire formatage(*;$currentObjectName_t))
			
			
			  //== OBJECT Get help tip ====================================================
			OB FIXER($currentObject_o;"helpTip";OBJET Lire message aide(*;$currentObjectName_t))
			
		Fin de si 
		  //== OBJECT Get horizontal alignment ========================================
		VCF_OBJ_SetPropWithLabel ($currentObjectName_t;"horizontalAlignment";$currentObject_o)
		
		
		  //== OBJECT Get indicator type ==============================================
		VCF_OBJ_SetPropWithLabel ($currentObjectName_t;"indicatorType";$currentObject_o)
		
		
		  //== OBJECT Get keyboard layout =============================================
		OB FIXER($currentObject_o;"keyboardLayout";OBJET Lire configuration clavier(*;$currentObjectName_t))
		
		
		  //== OBJECT Get list name ===================================================
		OB FIXER($currentObject_o;"listName";OBJET Lire nom liste(*;$currentObjectName_t))
		
		
		  //== OBJECT Get list reference ==============================================
		OB FIXER($currentObject_o;"choiceList";OBJET Lire liste référence(*;$currentObjectName_t;Liste énumération))
		OB FIXER($currentObject_o;"requiredList";OBJET Lire liste référence(*;$currentObjectName_t;Liste obligations))
		OB FIXER($currentObject_o;"excludedList";OBJET Lire liste référence(*;$currentObjectName_t;Liste exclusions))
		
		
		  //== OBJECT GET MAXIMUM VALUE ===============================================
		Au cas ou 
			: (($variableType_l=Est un texte) | ($variableType_l=Est un champ alpha))
				OBJET LIRE VALEUR MAXIMUM(*;$currentObjectName_t;$maximumValue_t)
				OB FIXER($currentObject_o;"maximumValue";$maximumValue_t)
			: ($variableType_l=Est une date)
				OBJET LIRE VALEUR MAXIMUM(*;$currentObjectName_t;$maximumValue_d)
				OB FIXER($currentObject_o;"maximumValue";$maximumValue_d)
			: ($variableType_l=Est une heure)
				OBJET LIRE VALEUR MAXIMUM(*;$currentObjectName_t;$maximumValue_h)
				OB FIXER($currentObject_o;"maximumValue";$maximumValue_h)
			Sinon 
				OBJET LIRE VALEUR MAXIMUM(*;$currentObjectName_t;$maximumValue_r)
				OB FIXER($currentObject_o;"maximumValue";$maximumValue_r)
		Fin de cas 
		
		
		  //== OBJECT GET MINIMUM VALUE ===============================================
		Au cas ou 
			: (($variableType_l=Est un texte) | ($variableType_l=Est un champ alpha))
				OBJET LIRE VALEUR MINIMUM(*;$currentObjectName_t;$minimumValue_t)
				OB FIXER($currentObject_o;"minimumValue";$minimumValue_t)
			: ($variableType_l=Est une date)
				OBJET LIRE VALEUR MINIMUM(*;$currentObjectName_t;$minimumValue_d)
				OB FIXER($currentObject_o;"minimumValue";$minimumValue_d)
			: ($variableType_l=Est une heure)
				OBJET LIRE VALEUR MINIMUM(*;$currentObjectName_t;$minimumValue_h)
				OB FIXER($currentObject_o;"minimumValue";$minimumValue_h)
			Sinon 
				OBJET LIRE VALEUR MINIMUM(*;$currentObjectName_t;$minimumValue_r)
				OB FIXER($currentObject_o;"minimumValue";$minimumValue_r)
		Fin de cas 
		
		
		  //== OBJECT Get name ========================================================
		  // Does not work with FORM LOAD
		
		
		  //== OBJECT Get placeholder =================================================
		OB FIXER($currentObject_o;"placeholder";OBJET Lire texte exemple(*;$currentObjectName_t))
		
		
		  //== OBJECT GET PRINT VARIABLE FRAME ========================================
		OBJET LIRE IMPRESSION TAILLE VARIABLE(*;$currentObjectName_t;$printFrame_f)
		OB FIXER($currentObject_o;"printFrame";$printFrame_f)
		VCF_OBJ_SetPropWithLabel ($currentObjectName_t;"subformPrintFrame";$currentObject_o)
		
		
		  //== OBJECT GET RESIZING OPTIONS ============================================
		VCF_OBJ_SetPropWithLabel ($currentObjectName_t;"horizontalSizing";$currentObject_o)
		VCF_OBJ_SetPropWithLabel ($currentObjectName_t;"verticalSizing";$currentObject_o)
		
		
		  //== OBJECT GET RGB COLORS ==================================================
		  // I want to make this useful as a pure text file, but it's a trade-off in terms of proper object design.
		  // If the value is negative, I save the 4D constant value and therefore also display a label.
		  // If the value is positive I use the 0x00RRGGBB hex template as a "label".
		OBJET LIRE COULEURS RVB(*;$currentObjectName_t;$foregroundColor_l;$backgroundColor_l;$altBackgrndColor_l)
		Si ($foregroundColor_l<0)
			VCF_OBJ_SetPropWithLabel ($currentObjectName_t;"foregroundColor";$currentObject_o)
		Sinon 
			OB FIXER($currentObject_o;"foregroundColor";$foregroundColor_l)
			OB FIXER($currentObject_o;"foregroundColor_hex";UTIL_CONV_RGBColorToText ($foregroundColor_l))
		Fin de si 
		Si ($backgroundColor_l<0)
			VCF_OBJ_SetPropWithLabel ($currentObjectName_t;"backgroundColor";$currentObject_o)
		Sinon 
			OB FIXER($currentObject_o;"backgroundColor";$backgroundColor_l)
			OB FIXER($currentObject_o;"backgroundColor_hex";UTIL_CONV_RGBColorToText ($backgroundColor_l))
		Fin de si 
		Si ($altBackgrndColor_l<0)
			VCF_OBJ_SetPropWithLabel ($currentObjectName_t;"altBackgroundColor";$currentObject_o)
		Sinon 
			OB FIXER($currentObject_o;"altBackgroundColor";$altBackgrndColor_l)
			OB FIXER($currentObject_o;"altBackgroundColor_hex";UTIL_CONV_RGBColorToText ($altBackgrndColor_l))
		Fin de si 
		
		
		  //== OBJECT GET SCROLL POSITION =============================================
		  // I don't think it makes to use this one if the form isn't being rendered. It also crashes 4D (ACI0083478, fixed after build 139873).
		  // OBJECT GET SCROLL POSITION(*;$currentObjectName_t;$verticalPosition_l;$horizontalPosition_l)
		
		
		  //== OBJECT GET SCROLLBAR ===================================================
		OBJET LIRE BARRES DÉFILEMENT(*;$currentObjectName_t;$horizontalScrollBar_f;$verticalScrollBar_f)
		OB FIXER($currentObject_o;"horizontalScrollBar";$horizontalScrollBar_f)
		OB FIXER($currentObject_o;"verticalScrollBar";$verticalScrollBar_f)
		
		
		  //== OBJECT GET SHORTCUT =============================================
		OBJET LIRE ÉQUIVALENT CLAVIER(*;$currentObjectName_t;$shortCutKey_t;$shortcutModifiers_l)  // 4th param no longer optional (ACI0083479)
		OB FIXER($currentObject_o;"shortcutKey";$shortCutKey_t)
		Si ($shortcutModifiers_l>0)
			VCF_OBJ_SetPropWithLabel ($currentObjectName_t;"shortcutKeyModifiers";$currentObject_o)
		Sinon 
			OB FIXER($currentObject_o;"shortcutModifiers";$shortcutModifiers_l)
		Fin de si 
		
		
		  //== OBJECT Get style sheet =============================================
		OB FIXER($currentObject_o;"styleSheet";OBJET Lire feuille de style(*;$currentObjectName_t))
		
		
		  //== OBJECT GET SUBFORM =============================================
		OBJET LIRE SOUS FORMULAIRE(*;$currentObjectName_t;$subformTable_p;$detailSubform_t;$listSubform_t)
		Si (Non(Pointeur nil($subformTable_p)))
			OB FIXER($currentObject_o;"source";Nom de la table($subformTable_p))
			OB FIXER($currentObject_o;"listForm";$listSubform_t)
			OB FIXER($currentObject_o;"detailForm";$detailSubform_t)
		Fin de si 
		
		
		  //== OBJECT GET SUBFORM CONTAINER SIZE ======================================
		  // Does not work with FORM LOAD, requires a "current object".
		
		
		  //== OBJECT Get text orientation ============================================
		OB FIXER($currentObject_o;"orientation";OBJET Lire orientation texte(*;$currentObjectName_t))
		
		
		  //== OBJECT Get three states checkbox =======================================
		OB FIXER($currentObject_o;"threeStates";OBJET Lire case à cocher trois états(*;$currentObjectName_t))
		
		
		  //== OBJECT Get title =======================================================
		OB FIXER($currentObject_o;"title";OBJET Lire titre(*;$currentObjectName_t))
		
		
		  //== OBJECT Get vertical alignment =============================================
		VCF_OBJ_SetPropWithLabel ($currentObjectName_t;"verticalAlignment";$currentObject_o)
		
		
		  //== OBJECT Get visible =====================================================
		OB FIXER($currentObject_o;"visible";OBJET Lire visible(*;$currentObjectName_t))
		
		
		  // All done!
		OB FIXER($currentObjects_o;$currentObjectName_t;$currentObject_o)
		
	Fin de boucle   // End loop over objects
	
	  // Add the objects to the form.
	OB FIXER($currentForm_o;"objects";$currentObjects_o)
	
	
Fin de si   // make sure there are some objects to save.

FORM LIBÉRER

$0:=$error_l
